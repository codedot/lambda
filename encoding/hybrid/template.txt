\app[\fan_{i}(a, b), \fan_{i}(c, d)] {
	/* Duplicate application. */
	++this.total;
} \fan_{i}[\app(a, c), \app(b, d)];

\fan_{i}[\lam(a, b), \lam(c, d)] {
	/* Duplicate abstraction. */
	++this.total;
} \lam[\fan_{i}(a, c), \fan_{i}(b, d)];

\fan_{i}[a, b] {
	/* Annihilate matching fans. */
	if (i == j)
		++this.total;
	else
		return false;
} \fan_{j}[a, b];

\fan_{i}[\fan_{j}(a, b), \fan_{j}(c, d)] {
	/* Duplicate different fans. */
	if (i != j)
		++this.total;
	else
		return false;
} \fan_{j}[\fan_{i}(a, c), \fan_{i}(b, d)];

\app[a, b] {
	/* Apply beta reduction. */
	++this.beta;
	++this.total;
} \lam[a, b];

\fan_{i}[\atom_{M}, \atom_{M}] {
	/* Duplicate an atom. */
	++this.total;
} \atom_{M};

\read_{C}[\fan_{i}(a, b)] {
	/* Duplicate context. */
	++this.total;
} \fan_{i}[\read_{C}(a), \read_{this.clone(C)}(b)];

\print {
	/* Output results of read-back. */
	this.nf = M;
	++this.total;
} \atom_{M};

\read_{C}[a] {
	/* Read back abstraction. */
	++this.total;
} \lam[\atom_{this.mkid()}, \read_{this.abst(C)}(a)];

\app[\read_{this.appl(M)}(a), a] {
	/* Read back application. */
	++this.total;
} \atom_{M};

\read_{C}[\atom_{this.atom(C, M)}] {
	/* Read back an atom. */
	++this.total;
} \atom_{M};

$$

INCONFIG

$$

READBACK

this.beta = 0;
this.total = 0;
