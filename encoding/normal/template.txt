\print {
	/* Output results of read-back. */
	this.nf = M;
	++this.total;
} \atom_{M};

\read_{C}[a] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(b, \read_{C}(a)), b];

\read_{C}[a] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(b, \read_{C}(a)), b];

\read_{C}[a] {
	/* Read back abstraction. */
	++this.total;
} \lambda[\atom_{this.mkid()}, \read_{this.abst(C)}(a)];

\lambda[\read_{this.appl(M)}(a), a] {
	/* Read back application. */
	++this.total;
} \atom_{M};

\read_{C}[\atom_{this.atom(C, M)}] {
	/* Read back an atom. */
	++this.total;
} \atom_{M};

\copy[\atom_{M}, \atom_{this.clone(M)}] {
	/* Copy an atom. */
	++this.total;
} \atom_{M};

\dup[\atom_{M}, \atom_{this.clone(M)}] {
	/* Duplicate an atom. */
	++this.total;
} \atom_{M};

\lambda[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \lambda(a, b)), c];

\lambda[a, b] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(c, \lambda(a, b)), c];

\lambda[a, b] {
	/* Apply a closed term. */
	++this.beta;
	++this.total;
} \lambda[a, b];

\copy[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \copy(a, b)), c];

\copy[a, b] {
	/* Initiate application. */
	++this.total;
} \apply[\lambda(c, \copy(a, b)), c];

\copy[\lambda(a, b), \lambda(c, d)] {
	/* Initiate copy of a closed term. */
	++this.total;
} \lambda[\dup(a, c), \dup(b, d)];

\dup[a, b] {
	/* Unshare variable. */
	++this.total;
} \share[\copy(c, \dup(a, b)), c];

\dup[a, b] {
	/* Duplicate sharing. */
	++this.total;
} \copy[\dup(\amb(c, \share(a, d), d), \amb(e, \share(b, f), f)), \dup(c, e)];

\dup[\apply(a, b), \apply(c, d)] {
	/* Duplicate application. */
	++this.total;
} \apply[\dup(a, c), \dup(b, d)];

\dup[\lambda(a, b), \lambda(c, d)] {
	/* Duplicate abstraction. */
	++this.total;
} \lambda[\dup(a, c), \dup(b, d)];

\dup[a, b] {
	/* Finish duplication. */
	++this.total;
} \dup[a, b];

\erase {
	/* Erase an atom. */
	++this.total;
} \atom_{M};

\erase {
	/* Erase sharing. */
	++this.total;
} \share[a, a];

\erase {
	/* Erase application. */
	++this.total;
} \apply[\erase, \erase];

\erase {
	/* Erase abstraction. */
	++this.total;
} \lambda[\erase, \erase];

\erase {
	/* Erase copy initiator. */
	++this.total;
} \copy[\erase, \erase];

\erase {
	/* Erase duplicator. */
	++this.total;
} \dup[\erase, \erase];

\erase {
	/* Finish erasing. */
	++this.total;
} \erase;

$$

INCONFIG

$$

READBACK

this.beta = 0;
this.total = 0;
