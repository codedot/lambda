\print {
	/* Continue evaluation. */
	++this.total;
} \wait[\print, \call];

\apply[a, \outapp(\outapp(b, c), a)] {
	/* Turn application. */
	++this.total;
} \outapp[b, c];

\fanin_{i}[a, b] {
	/* Duplicate application. */
	++this.total;
} \outapp[\fanin_{i}(\apply(c, a), \apply(d, b)), \fanin_{i}(c, d)];

\eval[\outapp(a, b)] {
	/* Postpone evaluation. */
	++this.total;
} \outapp[a, b];

\eval[\fanout_{i}(a, b)] {
	/* Postpone evaluation. */
	++this.total;
} \fanout_{i}[a, b];

\eval[a] {
	/* Continue evaluation. */
	++this.total;
} \wait[\eval(a), \call];

\eval[\atom_{M}] {
	/* Return an atom. */
	++this.total;
} \atom_{M};

\eval[\lambda(a, b)] {
	/* Return abstraction. */
	++this.total;
} \lambda[a, b];

\read_{C}[\fanout_{i}(a, b)] {
	/* Duplicate context. */
	++this.total;
} \fanout_{i}[\read_{C}(a), \read_{this.clone(C)}(b)];

\call {
	/* Erase late call. */
	++this.total;
} \erase;

\fanin_{i}[\wait(a, \amb(b, \decide(c, d), d)), \wait(e, b)] {
	/* Postpone duplication. */
	++this.total;
} \wait[\fanin_{i}(a, e), c];

\call {
	/* Continue evaluation. */
	++this.total;
} \decide[\call, \erase];

\erase {
	/* Erase reference. */
	++this.total;
} \decide[a, a];

\call {
	/* Continue evaluation. */
	++this.total;
} \hold[a, \eval(a)];

\read_{C}[\wait(a, b)] {
	/* Postpone readback. */
	++this.total;
} \wait[\read_{C}(a), b];

\erase {
	/* Erase holder. */
	++this.total;
} \hold[\erase, \erase];

\erase {
	/* Erase reference. */
	++this.total;
} \wait[\erase, \erase];

\apply[a, \wait(b, \hold(\apply(a, b), \wait(c, d)))] {
	/* Postpone application. */
	++this.total;
} \wait[c, d];

\print {
	/* Output results of read-back. */
	this.nf = M;
	++this.total;
} \atom_{M};

\read_{C}[a] {
	/* Read back abstraction. */
	++this.total;
} \lambda[\atom_{this.mkid()}, \read_{this.abst(C)}(a)];

\apply[\read_{this.appl(M)}(a), a] {
	/* Read back application. */
	++this.total;
} \atom_{M};

\read_{C}[\atom_{this.atom(C, M)}] {
	/* Read back an atom. */
	++this.total;
} \atom_{M};

\fanin_{i}[\atom_{M}, \atom_{M}] {
	/* Duplicate an atom. */
	++this.total;
} \atom_{M};

\apply[a, b] {
	/* Apply beta reduction. */
	++this.beta;
} \lambda[\wait(c, \hold(c, a)), b];

\apply[a, \outapp(\fanout_{i}(b, c), a)] {
	/* Turn application. */
	++this.total;
} \fanout_{i}[b, c];

\fanin_{i}[\lambda(a, b), \lambda(c, d)] {
	/* Duplicate abstraction. */
	++this.total;
} \lambda[\fanout_{i}(a, c), \fanin_{i}(b, d)];

\fanin_{i}[a, b] {
	/* Annihilate matching fans. */
	if (i == j)
		++this.total;
	else
		return false;
} \fanout_{j}[a, b];

\fanin_{i}[\fanout_{j}(a, b), \fanout_{j}(c, d)] {
	/* Duplicate different fans. */
	if (i != j)
		++this.total;
	else
		return false;
} \fanout_{j}[\fanin_{++this.id}(a, c), \fanin_{++this.id}(b, d)];

\erase {
	/* Erase an atom. */
	++this.total;
} \atom_{M};

\erase {
	/* Erase application. */
	++this.total;
} \apply[\erase, \erase];

\erase {
	/* Erase abstraction. */
	++this.total;
} \lambda[\erase, \erase];

\erase {
	/* Erase duplicator. */
	++this.total;
} \fanin_{i}[\erase, \erase];

\erase {
	/* Erase duplicator. */
	++this.total;
} \fanout_{j}[\erase, \erase];

\erase {
	/* Finish erasing. */
	++this.total;
} \erase;

\erase {
	/* Erase context. */
	++this.total;
} \read_{C}[\erase];

\erase {
	/* Erase application. */
	++this.total;
} \outapp[\erase, \erase];

$$

INCONFIG

$$

var id = 0;

function mkvar(fresh)
{
	if (fresh)
		++id;

	return "v" + id.toFixed(0);
}

function mkid(name)
{
	var obj = {
		node: "atom",
		name: name ? name : mkvar(true)
	};

	return obj;
}

function mkhole()
{
	var obj = {};

	obj.hole = obj;
	return obj;
}

function atom(context, obj)
{
	var hole = context.hole;
	var key;

	for (key in obj)
		hole[key] = obj[key];

	context.hole = obj.hole;
	return context;
}

function abst(context)
{
	var hole = mkhole();
	var obj = {
		node: "abst",
		var: mkvar(),
		body: hole,
		hole: hole
	};

	return atom(context, obj);
}

function appl(left)
{
	var context = mkhole();
	var hole = mkhole();
	var obj = {
		node: "appl",
		left: left,
		right: hole,
		hole: hole
	};

	return atom(context, obj);
}

function clone(obj, root, hole)
{
	var copy = {};

	if (!obj)
		return;

	if (!root) {
		root = copy;
		hole = obj.hole;
	}

	copy.node = obj.node;
	copy.var = obj.var;
	copy.name = obj.name;
	copy.body = clone(obj.body, root, hole);
	copy.left = clone(obj.left, root, hole);
	copy.right = clone(obj.right, root, hole);

	if (obj === hole)
		root.hole = copy;

	return copy;
}

this.id = 0;
this.clone = clone;
this.mkid = mkid;
this.mkvar = mkvar;
this.mkhole = mkhole;
this.abst = abst;
this.appl = appl;
this.atom = atom;
this.beta = 0;
this.total = 0;
